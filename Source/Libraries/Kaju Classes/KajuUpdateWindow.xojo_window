#tag DesktopWindow
Begin DesktopWindow KajuUpdateWindow
   Backdrop        =   0
   BackgroundColor =   &cFFFFFF00
   Composite       =   True
   DefaultLocation =   2
   FullScreen      =   False
   HasBackgroundColor=   False
   HasCloseButton  =   False
   HasFullScreenButton=   False
   HasMaximizeButton=   False
   HasMinimizeButton=   False
   Height          =   600
   ImplicitInstance=   True
   MacProcID       =   0
   MaximumHeight   =   32000
   MaximumWidth    =   32000
   MenuBar         =   0
   MenuBarVisible  =   True
   MinimumHeight   =   64
   MinimumWidth    =   64
   Resizeable      =   False
   Title           =   "#KajuLocale.kWindowTitle"
   Type            =   0
   Visible         =   True
   Width           =   800
   Begin DesktopHTMLViewer hvNotes
      AutoDeactivate  =   True
      Enabled         =   True
      Height          =   445
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Renderer        =   1
      Scope           =   2
      TabIndex        =   1
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   84
      Visible         =   True
      Width           =   631
   End
   Begin DesktopButton btnOK
      AllowAutoDeactivate=   True
      Bold            =   False
      Cancel          =   False
      Caption         =   "#KajuLocale.kInstallButton"
      Default         =   True
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   630
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MacButtonStyle  =   0
      Scope           =   2
      TabIndex        =   2
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   555
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   150
   End
   Begin DesktopButton btnCancel
      AllowAutoDeactivate=   True
      Bold            =   False
      Cancel          =   True
      Caption         =   "#KajuLocale.kRemindMeLaterButton"
      Default         =   False
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   468
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MacButtonStyle  =   0
      Scope           =   2
      TabIndex        =   3
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   555
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   150
   End
   Begin DesktopButton btnSkipVersion
      AllowAutoDeactivate=   True
      Bold            =   False
      Cancel          =   False
      Caption         =   "#KajuLocale.kSkipVersionButton"
      Default         =   False
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MacButtonStyle  =   0
      Scope           =   2
      TabIndex        =   4
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   555
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   150
   End
   Begin DesktopLabel lblMain
      AllowAutoDeactivate=   True
      Bold            =   True
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   False
      Scope           =   2
      Selectable      =   False
      TabIndex        =   5
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "Untitled"
      TextAlignment   =   0
      TextColor       =   &c00000000
      Tooltip         =   ""
      Top             =   14
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   631
   End
   Begin DesktopLabel lblSecondary
      AllowAutoDeactivate=   True
      Bold            =   False
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   25
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   True
      Scope           =   2
      Selectable      =   False
      TabIndex        =   6
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "Untitled"
      TextAlignment   =   0
      TextColor       =   &c00000000
      Tooltip         =   ""
      Top             =   39
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   631
   End
   Begin DesktopLabel Label1
      AllowAutoDeactivate=   True
      Bold            =   True
      Enabled         =   True
      FontName        =   "SmallSystem"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   False
      Scope           =   2
      Selectable      =   False
      TabIndex        =   7
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "#KajuLocale.kReleaseNotesLabel"
      TextAlignment   =   0
      TextColor       =   &c00000000
      Tooltip         =   ""
      Top             =   64
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   631
   End
   Begin DesktopLabel lblInstallMessage
      AllowAutoDeactivate=   True
      Bold            =   False
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   30
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   149
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   True
      Scope           =   2
      Selectable      =   False
      TabIndex        =   8
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "#KajuLocale.kDownloadingMessage"
      TextAlignment   =   0
      TextColor       =   &c00000000
      Tooltip         =   ""
      Top             =   555
      Transparent     =   True
      Underline       =   False
      Visible         =   False
      Width           =   294
   End
   Begin DesktopProgressBar pbProgress
      Active          =   False
      AllowAutoDeactivate=   True
      AllowTabStop    =   True
      Enabled         =   True
      Height          =   20
      Indeterminate   =   False
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   20
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      MaximumValue    =   100
      PanelIndex      =   0
      Scope           =   2
      TabIndex        =   8
      TabPanelIndex   =   0
      Tooltip         =   ""
      Top             =   555
      Transparent     =   False
      Value           =   0.0
      Visible         =   False
      Width           =   117
      _mIndex         =   0
      _mInitialParent =   ""
      _mName          =   ""
      _mPanelIndex    =   0
   End
   Begin Kaju.ZipShell shZipper
      Arguments       =   ""
      Backend         =   ""
      Canonical       =   False
      ExecuteMode     =   1
      ExitCode        =   0
      Index           =   -2147483648
      InitialParent   =   ""
      IsRunning       =   False
      LockedInPosition=   False
      PID             =   0
      Result          =   ""
      Scope           =   2
      TabPanelIndex   =   0
      TimeOut         =   0
   End
   Begin DesktopHTMLViewer hvNewWindow
      AutoDeactivate  =   True
      Enabled         =   True
      Height          =   200
      Index           =   -2147483648
      InitialParent   =   ""
      Left            =   -345
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Renderer        =   0
      Scope           =   2
      TabIndex        =   9
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   119
      Visible         =   False
      Width           =   300
   End
   Begin DesktopLabel lblVersions
      AllowAutoDeactivate=   True
      Bold            =   True
      Enabled         =   True
      FontName        =   "SmallSystem"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      Italic          =   False
      Left            =   20
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Multiline       =   False
      Scope           =   2
      Selectable      =   False
      TabIndex        =   10
      TabPanelIndex   =   0
      TabStop         =   True
      Text            =   "#KajuLocale.kVersionsLabel"
      TextAlignment   =   0
      TextColor       =   &c00000000
      Tooltip         =   ""
      Top             =   64
      Transparent     =   True
      Underline       =   False
      Visible         =   True
      Width           =   117
   End
   Begin DesktopPopupMenu pumUpdates
      AllowAutoDeactivate=   True
      Bold            =   False
      Enabled         =   True
      FontName        =   "System"
      FontSize        =   0.0
      FontUnit        =   0
      Height          =   20
      Index           =   -2147483648
      InitialParent   =   ""
      InitialValue    =   ""
      Italic          =   False
      Left            =   36
      LockBottom      =   False
      LockedInPosition=   False
      LockLeft        =   True
      LockRight       =   False
      LockTop         =   True
      Scope           =   2
      SelectedRowIndex=   0
      TabIndex        =   11
      TabPanelIndex   =   0
      TabStop         =   True
      Tooltip         =   ""
      Top             =   87
      Transparent     =   False
      Underline       =   False
      Visible         =   True
      Width           =   101
   End
   Begin Timer tmrTimeout
      Enabled         =   True
      Index           =   -2147483648
      InitialParent   =   ""
      LockedInPosition=   False
      Period          =   5000
      RunMode         =   0
      Scope           =   2
      TabPanelIndex   =   0
   End
   Begin Kaju.HTTPSocketAsync hsSocket
      Index           =   -2147483648
      LockedInPosition=   False
      Scope           =   2
      TabPanelIndex   =   0
      ValidateCertificates=   False
   End
End
#tag EndDesktopWindow

#tag WindowCode
	#tag Event
		Sub Closing()
		  if CurrentStage = Stage.Cancelled then
		    for each f as FolderItem in DeleteOnCancel
		      Kaju.DeleteRecursive( f )
		    next
		  end if
		  
		  for each f as FolderItem in DeleteOnClose
		    Kaju.DeleteRecursive( f )
		  next
		  
		  CurrentUpdate = nil
		  
		End Sub
	#tag EndEvent

	#tag Event
		Sub Opening()
		  RelativeToFolderItem = GetTemporaryFolderItem
		  DeleteOnClose.Append RelativeToFolderItem
		  
		  #if not TargetMacOS then
		    //
		    // Switch the buttons around for other platforms
		    //
		    dim farLeft as integer = btnCancel.Left
		    btnCancel.Left = btnOK.Left
		    btnOK.Left = farLeft
		    
		    const kAddition = 10
		    
		    btnCancel.Height = btnCancel.Height + kAddition
		    btnOK.Height = btnOK.Height + kAddition
		    btnSkipVersion.Height = btnSkipVersion.Height + kAddition
		    
		    //
		    // Make the pop-up menu bigger
		    //
		    pumUpdates.Height = pumUpdates.Height + kAddition
		    
		    self.Height = self.Height + kAddition
		    
		  #endif
		  
		End Sub
	#tag EndEvent

	#tag Event
		Sub Paint(g As Graphics, areas() As Rect)
		  //
		  // Draw a border around the release notes (Mac only)
		  //
		  
		  const kThickness = 1
		  
		  g.DrawPicture BackgroundImage, 0, 0, g.Width, g.Height
		  
		  dim drawLeft as integer = hvNotes.Left - kThickness
		  dim drawTop as integer = hvNotes.Top - kThickness
		  dim drawRight as integer = hvNotes.Left + hvNotes.Width
		  dim drawBottom as integer = hvNotes.Top + hvNotes.Height
		  dim drawWidth as integer = drawRight - drawLeft + kThickness
		  dim drawHeight as integer = drawBottom - drawTop + kThickness
		  
		  g.PenHeight = kThickness
		  g.PenWidth = kThickness
		  
		  g.ForeColor = &c00000000 // Black
		  g.DrawRect drawLeft, drawTop, drawWidth, drawHeight
		  
		  #if RBVersion > 2012.02 then
		    #pragma unused areas
		  #endif
		End Sub
	#tag EndEvent


	#tag Method, Flags = &h0
		Sub Cancel()
		  CurrentStage = Stage.Cancelled
		  
		  SelectedUpdate = nil
		  if Initiater <> nil then
		    Initiater.Cancel
		    Initiater = nil
		  end if
		  Kaju.CancelUpdate
		  
		  hsSocket.Disconnect
		  
		  if shZipper.IsRunning then
		    shZipper.Close
		  end if
		  
		  tmrTimeout.Mode = Timer.ModeOff
		  
		  if Checker.QuitOnCancelIfRequired and not Checker.DryRun and Checker.Result = Kaju.UpdateChecker.ResultType.RequiredUpdateAvailable then
		    quit
		  else
		    self.Close
		  end if
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ChangeBackgroudImage(update As Kaju.UpdateInformation)
		  dim useTransparency as boolean = update.UseTransparency
		  dim p as Picture = update.Image
		  if p is nil then
		    p = Checker.DefaultImage
		    useTransparency = Checker.DefaultUseTransparency
		  end if
		  
		  if p <> nil and useTransparency then
		    dim faded as new Picture( p.Width, p.Height )
		    
		    const kTransparencyPercent = 50.0
		    faded.Graphics.Transparency = kTransparencyPercent
		    
		    faded.Graphics.DrawPicture( p, 0, 0 )
		    
		    p = faded
		  end if
		  
		  self.BackgroundImage = p
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub ChooseUpdate(checker As Kaju.UpdateChecker, updates() As Kaju.UpdateInformation)
		  self.Checker = checker
		  CurrentStage = Stage.ChoosingUpdate
		  
		  //
		  // Sort the updates
		  //
		  dim vers() as double
		  for i as integer = 0 to updates.Ubound
		    vers.Append updates( i ).VersionAsDouble
		  next
		  
		  vers.SortWith( updates )
		  
		  //
		  // Reverse the sort
		  //
		  dim reverse() as integer
		  for i as integer = 0 to updates.Ubound
		    reverse.Append( updates.Ubound - i )
		  next
		  
		  reverse.SortWith( updates )
		  
		  //
		  // Set up the menu with the available updates.
		  // It will set up the rest of the controls.
		  //
		  
		  for i as integer = 0 to Updates.Ubound
		    dim update as Kaju.UpdateInformation = updates( i )
		    pumUpdates.AddRow update.Version
		    pumUpdates.RowTagAt( i ) = update
		  next
		  
		  pumUpdates.SelectedRowIndex = 0
		  
		  if updates.Ubound = 0 then
		    //
		    // Only one update so hide the menu
		    //
		    lblVersions.Visible = false
		    pumUpdates.Visible = false
		  end if
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CurrentUpdate_ImageReceived(sender As Kaju.UpdateInformation)
		  if sender is CurrentUpdate then
		    ChangeBackgroudImage( CurrentUpdate )
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub CurrentUpdate_ReleaseNotesReceived(sender As Kaju.UpdateInformation)
		  if sender is CurrentUpdate then
		    DisplayVersionInfo( CurrentUpdate )
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub DisplayVersionInfo(update As Kaju.UpdateInformation)
		  //
		  // Fill in the viewer
		  //
		  
		  self.Loading = true
		  
		  //
		  // Set up the main labels
		  //
		  AppName = update.AppName
		  if pumUpdates.RowCount = 1 then
		    lblSecondary.Text = KajuLocale.kSecondaryNoticeOne
		    lblSecondary.Text = lblSecondary.Text.ReplaceAll( KajuLocale.kNewVersionMarker, update.Version )
		  else
		    lblSecondary.Text = KajuLocale.kSecondaryNoticeMultiple
		  end if
		  
		  lblMain.Text = KajuLocale.kMainNotice
		  lblMain.Text = lblMain.Text.ReplaceAll( KajuLocale.kAppMarker, AppName )
		  lblSecondary.Text = lblSecondary.Text.ReplaceAll( KajuLocale.kThisVersionMarker, Kaju.AppVersionString )
		  
		  //
		  // Get the background picture, if any
		  //
		  ChangeBackgroudImage update
		  
		  //
		  // Show the release notes
		  //
		  dim source as string = update.DisplayReleaseNotes
		  hvNotes.LoadPage( source, RelativeToFolderItem )
		  
		  //
		  // hvNotes.CancelLoad will set self.Loading back to false
		  //
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub HandleOKButton()
		  select case CurrentStage
		  case Stage.ChoosingUpdate
		    //
		    // The update has been chosen
		    //
		    
		    SelectedUpdate = nil
		    SelectedBinary = nil
		    
		    if true then // Scope
		      dim chosen as Kaju.UpdateInformation = pumUpdates.RowTagAt( pumUpdates.SelectedRowIndex )
		      dim binary as Kaju.BinaryInformation = UserSelectsBinary( chosen )
		      if binary is nil then
		        return
		      end if
		      
		      if not UserConfirmsRequiredPayment( chosen ) then
		        return
		      end if
		      
		      SelectedUpdate = chosen
		      SelectedBinary = binary
		    end if
		    
		    btnOK.Enabled = false
		    
		    btnCancel.Caption = KajuLocale.kStopButton
		    
		    btnSkipVersion.Visible = false
		    pbProgress.Visible = true
		    lblInstallMessage.Visible = true
		    
		    pumUpdates.Enabled = false
		    
		    CurrentStage = Stage.InstallingUpdate
		    
		    if Checker.DryRun then
		      
		      lblInstallMessage.Text = KajuLocale.kDryRunMessage
		      
		    else
		      
		      lblInstallMessage.Text = KajuLocale.kDownloadingMessage
		      
		      dim tempFolder as FolderItem = Kaju.GetTemporaryFolder
		      DeleteOnCancel.Append tempFolder
		      
		      DownloadFile = tempFolder.Child( SelectedBinary.FileName )
		      DeleteOnClose.Append DownloadFile
		      
		      dim url as string = SelectedBinary.URL
		      
		      //
		      // Start the download
		      //
		      hsSocket.Get( url, DownloadFile )
		      
		      //
		      // Start the timeout timer
		      //
		      tmrTimeout.Reset
		      tmrTimeout.Mode = Timer.ModeSingle
		      
		    end if
		    
		  case Stage.WaitingToQuit
		    //
		    // The user chose Quit & Install
		    //
		    
		    Kaju.StartUpdate( self.Initiater )
		    
		    //
		    // Move this window to the back
		    //
		    dim lastWindowIndex as integer = WindowCount - 1
		    if not( Window( lastWindowIndex ) Is self ) then
		      dim showIndex as integer = lastWindowIndex
		      for windowIndex as integer = lastWindowIndex downto 0
		        dim w as Window = Window( showIndex )
		        if w Is self then
		          showIndex = showIndex - 1
		        else
		          w.Show
		        end if
		      next
		    end if
		    
		    Quit
		    
		  end
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub Show()
		  // Override super's show
		  
		  super.Show
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowError(msg As String = "")
		  if msg.Trim = "" then
		    msg = KajuLocale.kGenericErrorMessage
		  end if
		  
		  lblInstallMessage.Visible = true
		  lblInstallMessage.Text = msg
		  pbProgress.Visible = false
		  
		  btnOK.Enabled = false
		  btnCancel.Caption = KajuLocale.kTryLaterButton
		  btnSkipVersion.Visible = false
		  
		  CurrentStage = Stage.UpdateError
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowModal()
		  // Override super's ShowModal
		  
		  // Do nothing
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ShowModalWithin(parentWindow As DesktopWindow)
		  // Ovverride super's ShowModalWithin
		  
		  // Do nothing
		  
		  #pragma unused parentWindow
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function UserConfirmsRequiredPayment(update As Kaju.UpdateInformation) As Boolean
		  if update.RequiresPayment then
		    dim dlg as new MessageDialog
		    dlg.ActionButton.Visible = true
		    dlg.ActionButton.Caption = KajuLocale.kProceedButton
		    dlg.CancelButton.Visible = true
		    dlg.Message = KajuLocale.kPaymentRequiredMessage
		    dim btn as MessageDialogButton = dlg.ShowModal( self )
		    
		    if btn is dlg.CancelButton then
		      return false
		    end if
		  end if
		  
		  return true
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function UserSelectsBinary(update As Kaju.UpdateInformation) As Kaju.BinaryInformation
		  //
		  // Make sure we pick the right binary from the update
		  //
		  
		  if TargetMacOS or Target64Bit or not Checker.Allow32bitTo64bitUpdates then
		    return update.PlatformBinarySameBitness
		  elseif update.PlatformBinary64bit is nil then
		    return update.PlatformBinary32bit
		  else
		    //
		    // The current is 32-bit and either both binaries exist or only the 64-bit exists
		    // Let the user choose
		    //
		    
		    dim b32 as Kaju.BinaryInformation = update.PlatformBinary32bit
		    dim b64 as Kaju.BinaryInformation = update.PlatformBinary64bit
		    
		    dim msg as string 
		    if b32 is nil then
		      msg = KajuLocale.kChoose64bitMessage
		    else
		      msg = KajuLocale.kChooseBetweenBitsMessage
		    end if
		    msg = KajuLocale.kCurrenlyUsing32bitMessage + " " + msg
		    
		    dim dlg as new MessageDialog
		    dlg.Message = msg
		    dlg.Explanation = KajuLocale.kExplain64bitMessage
		    if b32 is nil then
		      dlg.ActionButton.Caption = KajuLocale.kProceedButton
		      dlg.ActionButton.Visible = true
		      dlg.AlternateActionButton.Visible = false
		    else
		      dlg.ActionButton.Caption = KajuLocale.kUse32bitLabel
		      dlg.ActionButton.Visible = true
		      dlg.AlternateActionButton.Caption = KajuLocale.kUse64bitLabel
		      dlg.AlternateActionButton.Visible = true
		    end if
		    dlg.CancelButton.Caption = KajuLocale.kCancelButton
		    dlg.CancelButton.Visible = true
		    
		    dim btn as MessageDialogButton = dlg.ShowModal( self )
		    if btn is nil or btn is dlg.CancelButton then
		      return nil
		    elseif b32 is nil or btn is dlg.AlternateActionButton then
		      return b64
		    else
		      return b32
		    end if
		  end if
		End Function
	#tag EndMethod


	#tag ComputedProperty, Flags = &h0
		#tag Getter
			Get
			  if mAppName = "" then
			    mAppName = KajuLocale.kThisApplication
			  end if
			  
			  return mAppName
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  mAppName = value
			End Set
		#tag EndSetter
		AppName As String
	#tag EndComputedProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  return mBackgroundImage
			  
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  mBackgroundImage = value
			  self.Refresh(True)
			End Set
		#tag EndSetter
		Private BackgroundImage As Picture
	#tag EndComputedProperty

	#tag Property, Flags = &h21
		Private Checker As Kaju.UpdateChecker
	#tag EndProperty

	#tag Property, Flags = &h21
		Private CurrentStage As Stage
	#tag EndProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  return mCurrentUpdate
			End Get
		#tag EndGetter
		#tag Setter
			Set
			  dim current as Kaju.UpdateInformation = mCurrentUpdate
			  if current isa object then
			    RemoveHandler current.ImageReceived, WeakAddressOf CurrentUpdate_ImageReceived
			    RemoveHandler current.ReleaseNotesReceived, WeakAddressOf CurrentUpdate_ReleaseNotesReceived
			  end if
			  
			  mCurrentUpdate = value
			  if value isa object then
			    AddHandler value.ImageReceived, WeakAddressOf CurrentUpdate_ImageReceived
			    AddHandler value.ReleaseNotesReceived, WeakAddressOf CurrentUpdate_ReleaseNotesReceived
			  end if
			  
			End Set
		#tag EndSetter
		Private CurrentUpdate As Kaju.UpdateInformation
	#tag EndComputedProperty

	#tag Property, Flags = &h21
		#tag Note
			Files/folders that shoudl be deleted if the user cancelled
		#tag EndNote
		Private DeleteOnCancel() As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		#tag Note
			Files/Folders that should be deleted upon window close
		#tag EndNote
		Private DeleteOnClose() As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private DownloadFile As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private Initiater As Kaju.UpdateInitiater
	#tag EndProperty

	#tag Property, Flags = &h21
		Private Loading As Boolean
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mAppName As String
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mBackgroundImage As Picture
	#tag EndProperty

	#tag Property, Flags = &h21
		Attributes( hidden ) Private mCurrentUpdate As Kaju.UpdateInformation
	#tag EndProperty

	#tag Property, Flags = &h21
		Private RelativeToFolderItem As FolderItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private SelectedBinary As Kaju.BinaryInformation
	#tag EndProperty

	#tag Property, Flags = &h0
		SelectedUpdate As Kaju.UpdateInformation
	#tag EndProperty


	#tag Enum, Name = Stage, Type = Integer, Flags = &h21
		ChoosingUpdate
		  InstallingUpdate
		  WaitingToQuit
		  UpdateError
		Cancelled
	#tag EndEnum


#tag EndWindowCode

#tag Events hvNotes
	#tag Event
		Function NewWindow(url as String) As DesktopHTMLViewer
		  return hvNewWindow
		End Function
	#tag EndEvent
	#tag Event
		Function CancelLoad(URL as String) As Boolean
		  #pragma unused URL
		  
		  dim r as boolean = not Loading
		  Loading = false
		  return r
		  
		End Function
	#tag EndEvent
	#tag Event
		Sub Error(error As RuntimeException)
		  '#pragma unused errorMessage
		  
		  #if TargetMacOS then
		    const kCancelledCode as integer = -999
		  #else
		    const kCancelledCode as integer = -9999999999
		  #endif
		  
		  if error.ErrorNumber <> kCancelledCode then
		    break
		  end if
		  
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events btnOK
	#tag Event
		Sub Pressed()
		  HandleOKButton
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events btnCancel
	#tag Event
		Sub Pressed()
		  self.Cancel
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events btnSkipVersion
	#tag Event
		Sub Pressed()
		  //
		  // We can only ignore versions if we already have the minimum requried
		  //
		  
		  dim info as Kaju.UpdateInformation = pumUpdates.RowTagAt( pumUpdates.SelectedRowIndex )
		  
		  if info.MinimumRequiredVersion <> "" and _
		    Kaju.VersionToDouble( Kaju.AppVersionString ) < Kaju.VersionToDouble( info.MinimumRequiredVersion ) then
		    dim msg as string = KajuLocale.kCannotSkipVersionsMessage
		    msg = msg.ReplaceAll( KajuLocale.kVersionMarker, info.MinimumRequiredVersion )
		    MessageBox msg
		    
		  else
		    
		    Checker.IgnoreVersion( info.Version )
		    
		    if pumUpdates.RowCount = 1 then
		      SelectedUpdate = nil
		      Kaju.CancelUpdate
		      self.Close
		    else
		      pumUpdates.RemoveRowAt( pumUpdates.SelectedRowIndex )
		      pumUpdates.SelectedRowIndex = 0
		    end if
		    
		  end if
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events shZipper
	#tag Event
		Sub DecompressCompleted(zipFile As FolderItem, containingFolder As FolderItem)
		  //
		  // No matter what, we don't need the zip file anymore
		  //
		  zipFile.Delete
		  
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  dim cnt as integer = containingFolder.Count
		  
		  if cnt = 0 or SelectedUpdate is nil then
		    
		    ShowError()
		    
		  else
		    
		    //
		    // Delete .DS_Store files
		    //
		    #if not TargetMacOS then
		      Kaju.DeleteDSStoreRecursive( containingFolder )
		    #endif
		    
		    //
		    // Find the executable
		    //
		    dim item as FolderItem
		    for i as integer = 1 to containingFolder.Count
		      dim f as FolderItem = containingFolder.Item( i )
		      dim name as string = f.Name
		      dim leftChars as string = name.Left( 1 )
		      if leftChars <> "." and leftChars <> "_" then
		        item = f
		        exit
		      end if
		    next
		    
		    if item is nil then
		      
		      ShowError()
		      
		    else
		      
		      Initiater = new Kaju.UpdateInitiater
		      Initiater.ReplacementAppFolder = item
		      Initiater.ReplacementExecutableName = SelectedBinary.ExecutableName
		      
		      btnOK.Enabled = true
		      btnOK.Caption = KajuLocale.kQuitButton
		      btnCancel.Visible = true
		      btnCancel.Caption = KajuLocale.kCancelButton
		      
		      pbProgress.Visible = false
		      
		      lblInstallMessage.Visible = true
		      lblInstallMessage.Text = KajuLocale.kReadyMessage
		      
		      CurrentStage = Stage.WaitingToQuit
		      
		    end if
		    
		  end if
		  
		End Sub
	#tag EndEvent
	#tag Event
		Sub Error()
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  ShowError()
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events hvNewWindow
	#tag Event
		Function CancelLoad(URL as String) As Boolean
		  ShowURL( URL )
		  return true
		  
		End Function
	#tag EndEvent
#tag EndEvents
#tag Events pumUpdates
	#tag Event
		Sub SelectionChanged(item As DesktopMenuItem)
		  //
		  // Fill in the viewer
		  //
		  
		  CurrentUpdate = nil
		  
		  if me.SelectedRowIndex = -1 then
		    if me.RowCount <> 0 then
		      me.SelectedRowIndex = 0
		    end if
		    return
		  end if
		  
		  dim update as Kaju.UpdateInformation = me.RowTagAt( me.SelectedRowIndex )
		  DisplayVersionInfo( update )
		  CurrentUpdate = update
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events tmrTimeout
	#tag Event
		Sub Action()
		  hsSocket.Disconnect
		  ShowError( KajuLocale.kTimedOutMessage )
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag Events hsSocket
	#tag Event
		Sub ReceiveProgress(BytesReceived as Int64, TotalBytes as Int64, NewData as xojo.Core.MemoryBlock)
		  #pragma unused newData
		  
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  //
		  // Have to get the value below 65536
		  //
		  
		  const kMaxAllowed = 1000
		  
		  if totalBytes > kMaxAllowed then
		    dim mult as integer = totalBytes \ kMaxAllowed
		    totalBytes = totalBytes \ mult
		    bytesReceived = bytesReceived \ mult
		  end if
		  
		  pbProgress.MaximumValue = totalBytes
		  pbProgress.Value = bytesReceived
		  
		  tmrTimeout.Reset
		  
		  
		End Sub
	#tag EndEvent
	#tag Event
		Sub Error(err as RuntimeException)
		  tmrTimeout.Mode = Timer.ModeOff
		  
		  dim errMsg as string = err.Message
		  if errMsg = "" then
		    errMsg = KajuLocale.kGenericErrorMessage
		  end if
		  
		  ShowError( errMsg + "  (" + str( err.ErrorNumber ) + ")" )
		  
		End Sub
	#tag EndEvent
	#tag Event
		Sub FileReceived(url As String, httpStatus As Integer, file As FolderItem)
		  #pragma unused url
		  
		  if CurrentStage = Stage.Cancelled then
		    //
		    // Do nothing
		    //
		    return
		  end if
		  
		  pbProgress.MaximumValue = -1
		  pbProgress.Value = 0
		  
		  tmrTimeout.Mode = Timer.ModeOff
		  
		  if httpStatus <> 200 then
		    
		    ShowError()
		    
		  elseif Kaju.HashOfFile( file ) <> SelectedBinary.Hash then
		    
		    ShowError( KajuLocale.kBadDownloadMessage )
		    
		  else
		    //
		    // We have the file and it appears to be good
		    //
		    
		    lblInstallMessage.Text = KajuLocale.kProcessingFileMessage
		    
		    dim targetFolder as FolderItem
		    #if TargetWindows then
		      dim targetFolderName as string = SelectedUpdate.AppName + "- decompressed"
		      targetFolder = App.ExecutableFile.Parent
		      targetFolder = targetFolder.Child( targetFolderName )
		      Kaju.DeleteRecursive( targetFolder )
		    #else
		      targetFolder = file.Parent.Child( "decompressed" )
		    #endif
		    DeleteOnCancel.Append targetFolder
		    shZipper.Decompress( file, targetFolder )
		  end if
		  
		End Sub
	#tag EndEvent
#tag EndEvents
#tag ViewBehavior
	#tag ViewProperty
		Name="MinimumWidth"
		Visible=true
		Group="Size"
		InitialValue="64"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MinimumHeight"
		Visible=true
		Group="Size"
		InitialValue="64"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MaximumWidth"
		Visible=true
		Group="Size"
		InitialValue="32000"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MaximumHeight"
		Visible=true
		Group="Size"
		InitialValue="32000"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Type"
		Visible=true
		Group="Frame"
		InitialValue="0"
		Type="Types"
		EditorType="Enum"
		#tag EnumValues
			"0 - Document"
			"1 - Movable Modal"
			"2 - Modal Dialog"
			"3 - Floating Window"
			"4 - Plain Box"
			"5 - Shadowed Box"
			"6 - Rounded Window"
			"7 - Global Floating Window"
			"8 - Sheet Window"
			"9 - Metal Window"
			"11 - Modeless Dialog"
		#tag EndEnumValues
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasCloseButton"
		Visible=true
		Group="Frame"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasMaximizeButton"
		Visible=true
		Group="Frame"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasMinimizeButton"
		Visible=true
		Group="Frame"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasFullScreenButton"
		Visible=true
		Group="Frame"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="DefaultLocation"
		Visible=true
		Group="Behavior"
		InitialValue="0"
		Type="Locations"
		EditorType="Enum"
		#tag EnumValues
			"0 - Default"
			"1 - Parent Window"
			"2 - Main Screen"
			"3 - Parent Window Screen"
			"4 - Stagger"
		#tag EndEnumValues
	#tag EndViewProperty
	#tag ViewProperty
		Name="HasBackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="BackgroundColor"
		Visible=true
		Group="Background"
		InitialValue="&hFFFFFF"
		Type="ColorGroup"
		EditorType="ColorGroup"
	#tag EndViewProperty
	#tag ViewProperty
		Name="AppName"
		Visible=false
		Group="Behavior"
		InitialValue=""
		Type="String"
		EditorType="MultiLineEditor"
	#tag EndViewProperty
	#tag ViewProperty
		Name="Backdrop"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="Picture"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Composite"
		Visible=true
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="FullScreen"
		Visible=false
		Group="Appearance"
		InitialValue="False"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Height"
		Visible=true
		Group="Position"
		InitialValue="400"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="ImplicitInstance"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Interfaces"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MacProcID"
		Visible=true
		Group="Appearance"
		InitialValue="0"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MenuBar"
		Visible=true
		Group="Appearance"
		InitialValue=""
		Type="DesktopMenuBar"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="MenuBarVisible"
		Visible=false
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Name"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Resizeable"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Super"
		Visible=true
		Group="ID"
		InitialValue=""
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Title"
		Visible=true
		Group="Appearance"
		InitialValue="Untitled"
		Type="String"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Visible"
		Visible=true
		Group="Appearance"
		InitialValue="True"
		Type="Boolean"
		EditorType=""
	#tag EndViewProperty
	#tag ViewProperty
		Name="Width"
		Visible=true
		Group="Position"
		InitialValue="600"
		Type="Integer"
		EditorType=""
	#tag EndViewProperty
#tag EndViewBehavior
